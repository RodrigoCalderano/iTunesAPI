apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    defaultConfig {
        applicationId "com.ciandt.book.seeker"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "APPCENTER_SECRET", keystoreProperties["APPCENTER_SECRET"]
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        animationsDisabled true
        execution "ANDROIDX_TEST_ORCHESTRATOR"
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        experimental = true
    }
}

def lifeCycleExtensionsVersion = '1.1.1'
def supportVersion = '28.0.0'
def retrofitVersion = '2.5.0'
def daggerVersion = '2.25.2'
def glideVersion = '4.8.0'
def mockitoVersion = '2.11.0'
def rxJavaVersion = '2.0.1'
def appCenterSdkVersion = '2.5.0'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // androidx dependencies
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // kotlin & coroutines dependencies
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.50'
    implementation 'org.jetbrains.kotlin:kotlin-android-extensions-runtime:1.3.50'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxJavaVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Support
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // LifeCycle
    implementation "android.arch.lifecycle:extensions:$lifeCycleExtensionsVersion"

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation 'android.arch.core:core-testing:1.1.1'

    // espresso
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // AppCenter
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.1"
}

detekt {
    toolVersion '1.0.1'
    input = files("src/main/java")
    config = files("$rootDir/default-detekt-config.yml")
    reports {
        xml { enabled = false }
        txt { enabled = false }
    }
}

configurations {
    jacocoReport
}

jacoco {
    toolVersion = "0.8.4"
}

task jacocoAndroidTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    group = "Reporting"
    description = "Generate unified Jacoco code coverage report"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def coverageTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/", excludes: fileFilter)
    def kotlinCoverageTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug/com/ciandt/book/seeker/", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/androidTest/java/com/ciandt/book/seeker/"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([coverageTree], [kotlinCoverageTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code_coverage/debugAndroidTest/connected/**/*.ec"
    ]))
}